// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in=instruction[15], out=isAIns);

    Mux16(a=aluOut, b=instruction, sel=isAIns, out=aIn);

    Or(a=isAIns, b=instruction[5], out=loadA); // load_a = is_a_ins OR save_to_a
    ARegister(in=aIn, load=loadA, out=aOut, out[0..14]=addressM);

    And(a=instruction[15], b=instruction[12], out=selm); // sel_m = is_c_ins && use_m
    Mux16(a=aOut, b=inM, sel=selm, out=aOrMem); // A OR M

    And(a=instruction[15], b=instruction[4], out=loadd); // load_d = is_c_instr AND save_to_d
    DRegister(in=aluOut, load=loadd, out=dOut);

    ALU(x=dOut, y=aOrMem, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6],
    out=aluOut, out=outM, zr=zero, ng=negative);

    Or(a=negative, b=zero, out=negorzero);
    Not(in=negorzero, out=positive);
    And(a=positive, b=instruction[0], out=jumppositive);
    And(a=zero, b=instruction[1], out=jumpzero);
    And(a=negative, b=instruction[2], out=jumpnegative);
    Or8Way(in[0]=jumppositive, in[1]=jumpzero, in[2]=jumpnegative, out=jumpmaybe);
    And(a=instruction[15], b=jumpmaybe, out=loadpc);
    PC(in=aOut, reset=reset, load=loadpc, inc=true, out[0..14]=pc);

    And(a=instruction[15], b=instruction[3], out=writeM);
}